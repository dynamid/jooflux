/* ..................................................................................... #
#
# JooFlux
#
# Copyright (c) 2012 Institut National des Sciences Appliquées de Lyon (INSA-Lyon)
# Copyright (c) 2012 Julien Ponge, INSA-Lyon
# Copyright (c) 2012 Frédéric Le Mouël, INSA-Lyon
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at http://mozilla.org/MPL/2.0/.
#
# ..................................................................................... #
# JooFlux
# ..................................................................................... */

allprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.7

    defaultTasks 'clean', 'build'

    ext {
        JOOFLUX_VERSION = 'r2-SNAPSHOT'
        JOOFLUX_LOGGING = System.env.JOOFLUX_LOGGING ? System.env.JOOFLUX_LOGGING : ''
        LOGGING_FLAG = "-verbose:class ${JOOFLUX_LOGGING}"
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        compile 'org.ow2.asm:asm-all:4.0'
    }
}

version = JOOFLUX_VERSION

dependencies {
    compile 'org.tinylog:tinylog:0.8'
    compile 'com.googlecode.json-simple:json-simple:1.1.1'
    testCompile 'junit:junit:4.10'
    testCompile 'org.powermock:powermock-api-mockito:1.6.1'
    testCompile 'org.powermock:powermock-module-junit4:1.6.1'
}

jar {
    from("LICENSE") {
        into "META-INF"
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest {
        attributes(
                "Specification-Title": 'Jooflux',
                "Specification-Version": JOOFLUX_VERSION,
                "Specification-Vendor": "INSA-Lyon",
                "Implementation-Title": "JooFlux",
                "Implementation-Version": JOOFLUX_VERSION,
                "Implementation-Vendor": "INSA-Lyon",
                "Copyright": 'Copyright (c) 2012 INSA-Lyon',
                "License": 'Mozilla Public License Version 2.0',
                "License-URL": 'http://www.mozilla.org/MPL/2.0/',
                "Premain-Class": 'fr.insalyon.telecom.jooflux.InvokeInterceptorAgent',
                "Can-Redefine-Classes": 'false',
                "Can-Retransform-Classes": 'false',
                "Can-Set-Native-Method-Prefix": 'false'
        )
    }
}
